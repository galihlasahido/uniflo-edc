apply plugin: 'com.android.application'
static def releaseDate() {
    return new Date().format("yyyyMMdd-HH:mm", TimeZone.getTimeZone("GMT+8"))
}
android {
    compileSdk 29
    // buildToolsVersion "29.0.3" // Let Gradle use default for Android Plugin 4.2.2
    defaultConfig {
        applicationId "id.uniflo.uniedc"
        minSdkVersion 22
        targetSdkVersion 29
        versionCode 101110000
        versionName "V1.0.1.11_00_${releaseDate()}.00"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "BUILD_TIMESTAMP", getDate()
    }
    signingConfigs {
        release {
            storeFile file("../keystore/feitian/ftdemo.jks")
            storePassword "123456"
            keyAlias "ftdemo"
            keyPassword "123456"
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    buildFeatures {
        buildConfig true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            def apkName = "uniedc-${defaultConfig.versionName}.apk"
            outputFileName = apkName
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        disable 'ObsoleteLintCustomCheck'
    }
    packagingOptions {
        pickFirst 'lib/arm64-v8a/libsqlcipher.so'
        pickFirst 'lib/armeabi-v7a/libsqlcipher.so'
        pickFirst 'lib/x86/libsqlcipher.so'
        pickFirst 'lib/x86_64/libsqlcipher.so'
    }
}

String getDate() {
    Date date = new Date();
    String dates = "\"" + date.format("yyyy-MM-dd HH:mm:ss", TimeZone.getTimeZone("GMT+8")) + "\"";
    return dates;
}

tasks.register("runApp") {
    dependsOn "installDebug"
    doLast {
        def pkg = "id.uniflo.uniedc"
        def activity = ".ui.CardReaderActivity"
        println "Starting ${pkg}/${activity}..."
        exec {
            commandLine "adb", "shell", "am", "start", "-n", "${pkg}/${activity}"
        }
        println "Waiting for app to start..."
        Thread.sleep(2000) // Wait 2 seconds for app to fully start
        println "Clearing logcat..."
        exec {
            commandLine "adb", "logcat", "-c"
        }
        println "Listening to logcat for CardReaderActivity..."
        exec {
            commandLine "adb", "logcat", "-v", "time", "CardReaderActivity:D", "*:S"
        }
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'com.google.android.material:material:1.4.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'com.google.zxing:core:3.3.3'
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0'
    implementation 'net.zetetic:android-database-sqlcipher:4.4.3'
    implementation 'androidx.sqlite:sqlite:2.1.0'
    implementation 'org.json:json:20210307'
    
    // Room database - temporarily disabled
    // implementation 'androidx.room:room-runtime:2.2.6'
    // annotationProcessor 'androidx.room:room-compiler:2.2.6'
}
